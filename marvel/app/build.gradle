plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

def localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.marvel"
        minSdk 26
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    flavorDimensions "environment"
    productFlavors{
        development {
            dimension "environment"
            applicationIdSuffix = ".dev"
            versionNameSuffix '-dev'
            ext {
                BASE_URL = "http://gateway.marvel.com"
            }
        }

        recette {
            dimension "environment"
            applicationIdSuffix = ".recette"
            versionNameSuffix '-recette'
            ext {
                BASE_URL = "https://46ca812c-7d96-4d73-8cbe-e1aeefccd732.mock.pstmn.io"
            }
        }


        applicationVariants.all { variant ->

            def dimensions = variant.productFlavors
            def environment = dimensions[0]

            def BASE_URL = environment.ext.BASE_URL
            variant.buildConfigField "String", "BASE_URL", "\"$BASE_URL\""

        }
    }

    buildTypes {
        debug {
            buildConfigField("String", "marvel_api_private", localProperties["marvelAPIPrivate"])
            buildConfigField("String", "marvel_api_public", localProperties["marvelAPIPublic"])

        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

}

dependencies {
    // Add this line for the QR code Scanner
    implementation "com.journeyapps:zxing-android-embedded:3.6.0"
    implementation 'com.budiyev.android:code-scanner:2.1.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2-native-mt"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.0'

    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'

    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation("com.squareup.okhttp3:logging-interceptor:4.9.2")
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"


    def versionVM = "2.4.0"
    //lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$versionVM"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$versionVM"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$versionVM"

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    testImplementation 'junit:junit:'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}